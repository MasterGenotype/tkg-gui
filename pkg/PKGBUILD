# Maintainer: superphenotype
pkgname=tkg-gui-git
pkgver=1.2.3.r1.gafba3a2
pkgrel=1
pkgdesc="Graphical interface for building custom Linux kernels using the linux-tkg build system"
arch=('x86_64')
url="https://github.com/MasterGenotype/tkg-gui"
license=('unknown')
depends=('gcc-libs' 'glibc' 'libxkbcommon' 'libxcb' 'wayland' 'mesa')
makedepends=('cargo' 'git')
optdepends=(
    'base-devel: required by makepkg for kernel builds'
)
provides=('tkg-gui')
conflicts=('tkg-gui')
source=("tkg-gui::git+ssh://git@github.com/MasterGenotype/tkg-gui.git")
sha256sums=('SKIP')

pkgver() {
    cd tkg-gui
    git describe --long --tags --abbrev=7 2>/dev/null \
        | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g' \
        || printf "0.1.0.r%s.g%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

prepare() {
    cd tkg-gui
    git submodule update --init --recursive
    export RUSTUP_TOOLCHAIN=stable
    cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
    cd tkg-gui
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --frozen --release
}

package() {
    cd tkg-gui
    install -Dm755 "target/release/tkg-gui" "$pkgdir/usr/bin/tkg-gui"

    # Install submodules to /usr/share/tkg-gui so the binary can locate them
    install -dm755 "$pkgdir/usr/share/tkg-gui"
    cp -a submodules/linux-tkg "$pkgdir/usr/share/tkg-gui/linux-tkg"

    # Docs
    install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}
